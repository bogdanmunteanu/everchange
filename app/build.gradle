apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'eu.davidea.grabver'

ext.versionMajor = 1
ext.versionMinor = 0
ext.versionPatch = 0
ext.versionClassifier = null
ext.isSnapshot = false

repositories {
    google()
    jcenter()

    maven { url "http://dl.bintray.com/davideas/maven" }
    maven { url 'https://plugins.gradle.org/m2/' }
    maven { url 'http://repo1.maven.org/maven2' }
    maven { url 'http://jcenter.bintray.com' }


}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "ro.bogdanmunteanu.currencyconverter"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode generateVersionCode() // 190010203
        versionName generateVersionName() // 1.2.3-SNAPSHOT
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

    }
    androidExtensions {
        experimental = true
    }

    applicationVariants.all { variant ->
        customApkName(variant)
    }
}

versioning {
    // required (number)
    major = 1
    minor = 0
    patch = 0
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    //Multidex
    implementation 'com.android.support:multidex:1.0.3'

    // retrofit + gson
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // rx
    implementation "io.reactivex.rxjava2:rxjava:$rxjava2_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"

    // dependency injection
    implementation "com.google.dagger:dagger:$dagger2_version"
    implementation "com.google.dagger:dagger-android:$dagger2_version"
    implementation "com.google.dagger:dagger-android-support:$dagger2_version"
    kapt "com.google.dagger:dagger-compiler:$dagger2_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger2_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger2_version"
    implementation 'javax.annotation:jsr250-api:1.0'
    implementation 'javax.inject:javax.inject:1'

    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:$okhttp"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:3.10.0"

    // ui
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:design:$support_version"

    //lifecycle components
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

    //circle image view
    implementation "de.hdodenhof:circleimageview:3.0.0"

    //glide for image loading
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'

    //reflection
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    //test
    testImplementation group: 'android.arch.core', name: 'core-testing', version: '1.1.1'
    testImplementation "org.mockito:mockito-core:2.19.0"
    testImplementation "io.mockk:mockk:1.9.3"
}

kapt {
    generateStubs = true
}

def customApkName(variant) {
    variant.outputs.all { output ->

        def newApkName
        newApkName = output.outputFile.name //this is causing the sync warning
        newApkName = newApkName.replace("app-", "CurrencyConverter-")

        outputFileName = newApkName.replace(".apk", "-${versioning.major}.${versioning.minor}.${versioning.patch}.apk")
    }
}

private Integer generateVersionCode() {
    return ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null && ext.isSnapshot) {
        ext.versionClassifier = "SNAPSHOT"
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}


